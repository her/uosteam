//  Name: Halygon's Dual Mob Provoke Macro
//  Description: This macro selects the two nearest mobs and
//  provokes them upon one another. No selection is required,
//  only set this macro to a hotkey. Be aware that you do not
//  have control over who it targets if multiple mobs are equally in range.
// Create Instrument List if it does not exist
if not listexists 'instrumentlist'
  @createlist 'instrumentlist'
endif
// Clear list -- in case you have added additional instrument types
@clearlist 'instrumentlist'
// Add below different types of instruments to use -- Common Ones are Added already
@pushlist 'instrumentlist' 0xeb1  // Standing Harp
@pushlist 'instrumentlist' 0xeb2  // Lap Harp
@pushlist 'instrumentlist' 0xeb3  // Lute
@pushlist 'instrumentlist' 0xe9c  // Drum
@pushlist 'instrumentlist' 0xe9d  // Tambourine
@pushlist 'instrumentlist' 0xe9e  // Tambourine with red tassle
// Select an instrument automatically to use
for 0 to 'instrumentlist'
  @findtype instrumentlist[]
  @setalias 'instrument' 'found'
endfor
// Clear Mobs
unsetalias 'provoke1'
unsetalias 'provoke2'
// Get first mob
if getenemy 'gray' 'enemy' 'nearest'
  @setalias 'provoke1' 'enemy'
else
  sysmsg 'No Mobs within range to provoke'
endif
// Get Second mob - because 'nearest' is used, it will not select the same mob as above as nearest cycles through the two closest mobs
if getenemy 'gray' 'enemy' 'nearest'
  @setalias 'provoke2' 'enemy'
else
  sysmsg 'No Mobs within range to provoke'
endif
// Clear journal to prepare for instrument check
clearjournal
useskill 'Provocation'
pause 1000
// If instrument has not been used since login, this will find the message asking to select an instrument and target the pre-selected instrument from above
if @injournal 'What instrument shall' 'system'
  target! 'instrument'
endif
waitfortarget 1500
target! 'provoke1'
waitfortarget 1500
target! 'provoke2'
